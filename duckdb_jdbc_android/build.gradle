plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'org.duckdb.duckdb_jdbc'
    compileSdk 33

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    defaultConfig {
        minSdk 24
        versionCode 1
        versionName '1.0'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        externalNativeBuild {
            cmake {
                arguments(
                    '-DJDBC_DRIVER=1',
                    '-DEXTENSION_STATIC_BUILD=1',
                    '-DOVERRIDE_JDBC_OS_ARCH=arm64',
                    '-DANDROID_ARM_MODE=arm',
                    '-DANDROID_STL=c++_shared',
                    '-DENABLE_SANITIZER=FALSE',
                    '-DENABLE_UBSAN=0'
                )

                targets 'duckdb_java'
                cppFlags '-std=c++11'
                abiFilters 'arm64-v8a'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    externalNativeBuild {
        cmake {
            path file('../../../CMakeLists.txt')
            version '3.22.1'
        }
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'org.duckdb'
                artifactId 'duckdb_jdbc_android'
                version '0.8.1'
            }
        }
    }
}
